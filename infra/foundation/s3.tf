# The lake bucket: Iceberg tables, Spark checkpoints, producer state, Athena results and
# the dashboard snapshots. No versioning: Iceberg snapshots already keep history, and
# versioning would double storage after every compaction.

locals {
  athena_results_prefix = "athena-results/"
  checkpoints_prefix    = "_checkpoints/"
  dashboard_prefix      = "dashboard/"
}

resource "aws_s3_bucket" "lake" {
  bucket_prefix = "wikiwatch-lake-"

  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "lake" {
  bucket = aws_s3_bucket.lake.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "lake" {
  bucket = aws_s3_bucket.lake.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_ownership_controls" "lake" {
  bucket = aws_s3_bucket.lake.id

  rule {
    object_ownership = "BucketOwnerEnforced"
  }
}

resource "aws_s3_bucket_lifecycle_configuration" "lake" {
  bucket = aws_s3_bucket.lake.id

  rule {
    id     = "expire-athena-results"
    status = "Enabled"

    filter {
      prefix = local.athena_results_prefix
    }

    expiration {
      days = 7
    }
  }

  # Checkpoints are per session (invariant 6) and never reused, so old ones are garbage.
  rule {
    id     = "expire-checkpoints"
    status = "Enabled"

    filter {
      prefix = local.checkpoints_prefix
    }

    expiration {
      days = 14
    }
  }

  rule {
    id     = "abort-incomplete-uploads"
    status = "Enabled"

    filter {}

    abort_incomplete_multipart_upload {
      days_after_initiation = 1
    }
  }
}

data "aws_iam_policy_document" "lake_tls_only" {
  statement {
    sid       = "DenyInsecureTransport"
    effect    = "Deny"
    actions   = ["s3:*"]
    resources = [aws_s3_bucket.lake.arn, "${aws_s3_bucket.lake.arn}/*"]

    principals {
      type        = "*"
      identifiers = ["*"]
    }

    condition {
      test     = "Bool"
      variable = "aws:SecureTransport"
      values   = ["false"]
    }
  }
}

resource "aws_s3_bucket_policy" "lake" {
  bucket = aws_s3_bucket.lake.id
  policy = data.aws_iam_policy_document.lake_tls_only.json

  depends_on = [aws_s3_bucket_public_access_block.lake]
}
